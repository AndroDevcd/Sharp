mod std.io.task;

import std.io;

class job_master {

	job_master() {
		long_term_pool = new thread_group("Long term jobs", min_priority);
		short_term_pool = new thread_group("Short term jobs", max_priority);
		job_queue = new synced_list<job>();
		controllers = new hashtable<thread, job_controller>();
	}

	def operator=(main: ()()) : job {
	    scheduled_job := create_job();
		scheduled_job = main;
		start_job(scheduled_job);
		return scheduled_job;
	}

	protected def create_job() := new job("job#${job.guid}");

	protected def create_job(name : string) := new job(name);

	protected def start_job(scheduled_job : job) {
	    lock(self) {
            if(!started) {
                start_scheduler();
                started = true;
            }
		}

		if(scheduled_job.delay_time)
			scheduled_job.delay_time = scheduled_job.delay_time + time.mills_time();

		if(scheduled_job.block_caller)
			execute_synchronous(scheduled_job);
		else {
			// the job scheduler will perform the start
			job_queue.add(scheduled_job);
		}
	}

	protected def get_host_controller(host: thread) := controllers.at(host);

	protected def cleanup_job(scheduled_job : job) {
		scheduled_job.deferred_obj = null;
		scheduled_job.host = null;
		scheduled_job.next_task = null;
		scheduled_job.args = null;
		scheduled_job.name = null;

        wakeup:
        lock(scheduled_job) {
            if(!scheduled_job.listeners.empty()) {
                foreach(t in scheduled_job.listeners) {
                    t.notify();
                }
            }

            scheduled_job.listeners.clear();
        }

        lock(scheduled_job) {
            if(!scheduled_job.listeners.empty()) {
                goto wakeup;
            }
        }

        scheduled_job.listeners = null;
		job_queue.remove(scheduled_job, { it, it2 -> return it.id == it2.id; });
	}

	protected def cancel_jobs_by_name(name: string) {
	   lock(job_queue) {
           for(i := 0; i < job_queue.size(); i++) {
              if(job_queue.at(i).name == name) {
                 job_queue.remove_at(i);
                 i = 0;
              }
           }

           foreach(t in short_term_pool) {
               schdj := get_host_controller(t).scheduled_job;
               if(schdj != null && schdj.name == name) {
                  schdj.cancel();
               }
           }

           foreach(t in long_term_pool) {
               if(t == scheduler) continue;

               schdj := get_host_controller(t).scheduled_job;
               if(schdj != null && schdj.name == name) {
                  schdj.cancel();
               }
           }
       }
	}

	protected job_queue : synced_list<job>;
	protected controllers : hashtable<thread, job_controller>;
	protected long_term_pool : thread_group;
	protected short_term_pool : thread_group;
	protected started: var;
}
