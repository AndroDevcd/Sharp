mod app;

import std;

/**                          
* Created by bknun on 6/13/2018            
* Description:                             
*/
class Tokenizer {

    public Tokenizer(string tokens, string file)
    {
        line = 1;
        self->file = file;
        entities = new ArrayList<TokenEntity>();
        lines = new ArrayList<string>();

        if(!tokens.empty())
            self->tokens = tokens;
        else {
            line = 0;
        }

        parse();
    }

    def parse() {
        if(tokens.empty())
            return;

        addLines();
        errors = new ErrorManager(lines, file, true, Options.aggressiveErrors);
        current = tokens.at(0);

        while(!isEnd()) {
            start = cursor;
            switch (current) {
              case '(': addToken(TokenType.LEFT_PAREN); break;
              case ')': addToken(TokenType.RIGHT_PAREN); break;
              case '{': addToken(TokenType.LEFT_CURLY); break;
              case '}': addToken(TokenType.RIGHT_CURLY); break;
              case '[': addToken(TokenType.LEFT_BRACE); break;
              case ']': addToken(TokenType.RIGHT_BRACE); break;
              case ',': addToken(TokenType.COMMA); break;
              case '.': addToken(TokenType.DOT); break;
              case ';': addToken(TokenType.SEMICOLON); break;
              case ':': addToken(TokenType.COLON); break;
              case '#': addToken(TokenType.HASH); break;
              case '?': addToken(TokenType.QUESMK); break;
              case '$': addToken(TokenType.DOLLAR); break;
              case '!': addToken(match('=') ? TokenType.NOTEQ : TokenType.NOT); break;
              case '=': addToken(match('=') ? TokenType.EQEQ : TokenType.ASSIGN); break;
              case '<':
                if(match('<'))
                    addToken(TokenType.SHL);
                else
                    addToken(match('=') ? TokenType._LTE : TokenType.LESSTHAN);
                break;
              case '>':
                if(match('>'))
                    addToken(TokenType.SHR);
                else
                    addToken(match('=') ? TokenType._GTE : TokenType.GREATERTHAN);
                break;

              case '&':
                if(match('='))
                    addToken(TokenType.ANDEQ);
                else
                    addToken(match('&') ? TokenType.ANDAND : TokenType.AND);
                break;
              case '|':
                if(match('='))
                    addToken(TokenType.OREQ);
                else
                    addToken(match('|') ? TokenType.OROR : TokenType.OR);
                break;
              case '^': addToken(match('=') ? TokenType.XOREQ : TokenType.XOR); break;
              case '%': addToken(match('=') ? TokenType.MODEQ : TokenType._MOD); break;
              case '+':
                if(match('='))
                    addToken(TokenType.PLUSEQ);
                else
                    addToken(match('+') ? TokenType._INC : TokenType.PLUS);
                break;
              case '-':
                if(match('='))
                    addToken(TokenType.MINUSEQ);
                else if(match('>'))
                    addToken(TokenType.PTR);
                else
                    addToken(match('-') ? TokenType._DEC : TokenType.MINUS);
                break;
              case '*': addToken(peek(1)=='=' ? TokenType.MULTEQ : TokenType.MULT); break;
              case ' ':
              case '\t':
                // Ignore whitespace.
                break;
              case '/':
                if (match('/')) {
                  // A comment goes until the end of the line.
                  while (!char.isNewLine(peek(1)) && !isEnd()) advance();
                } else if(match('*')) {
                  // A multiline comment goes until the [*/].
                  while(!isEnd() && (current != '*' && peek(1) != '/'))
                  {
                      if(char.isNewLine(current)){
                          newline();
                      }

                      advance();
                  }
                } else {
                    if(match('='))
                        addToken(TokenType.DIVEQ);
                    else
                        addToken(TokenType._DIV);
                }
                break;

              case '\r':
              case '\n':
                line++;
                col = 0;
                break;

              case '"':
                parseString();
                break;

              default:
                if(char.isDigit(current)) {
                    parseNumber();
                } else if (char.isAlpha(current) || current == '_') {
                    parseIdentifier();
                } else if(current == '\'') {
                    parseChar();
                } else
                    errors.createNewError(CompilerError.UNEXPECTED_SYMBOL, line, col, " `" + new char(current) + "`");
                break;
            }

            advance();
        }

//        TokenEntity[] arry = entities.toArray();
//        foreach(TokenEntity token : arry) {
//            System.println(" token: " + token.getToken()
//                + " - " + token.getId() + " line " + token.getLine()
//                + " col " + token.getCol());
//        }

        end:
        entities.add(new TokenEntity("eof", TokenId.SINGLE, TokenType._EOF, 0, line));
    }

    private def addToken(TokenId id) {
      string text = tokens.substring(start, cursor);
      entities.add(new TokenEntity(text, id, col, line));
    }

    private def addToken(TokenType type) {
      string text = tokens.substring(start, cursor);
      entities.add(new TokenEntity(text, TokenId.SINGLE, type, col, line));
    }

    private def match(var c) : var {
        if(peek(1)==c) {
            advance();
            return true;
        }

        return false;
    }

    private def parseChar() {
        if (tokensLeft() < 2)
        {
            errors.createNewError(CompilerError.EXPECTED_CHAR_LITERAL_EOF, line, col);
            advance();
            return;
        }
        advance();

        start = cursor;
        var escapedFound  = false;
        var escaped       = false;
        var hasCharacter  = false;

        while (!isEnd())
        {
            if (!escaped && ('\\' == current))
            {
                if(hasCharacter)
                {
                    errors.createNewError(CompilerError.ILLEGAL_CHAR_LITERAL_FORMAT, line, col, ", a chacacter literal cannot contain more than a single character; expected `'`");
                    return;
                }

                escapedFound = true;
                escaped = true;
                advance();
                continue;
            }
            else if (!escaped)
            {
                if ('\'' == current)
                    break;

                if(hasCharacter)
                {
                    errors.createNewError(CompilerError.ILLEGAL_CHAR_LITERAL_FORMAT, line, col, ", a chacacter literal cannot contain more than a single character; expected `'`");
                    return;
                }

                hasCharacter = true;
            }
            else if (escaped)
            {
                hasCharacter = true;
                if(!char.isAlpha(current) && current != '\\' && current != '\"' && current != '\'')
                {
                    errors.createNewError(CompilerError.ILLEGAL_CHAR_LITERAL_FORMAT, line, col, ", text preceding `\\` must be alpha, '\"', or '\\'' only");
                    return;
                }
                escaped = false;
            }
            advance();
        }

        if (isEnd())
        {
            errors.createNewError(CompilerError.UNEXPECTED_EOF, line, col);
            return;
        }

        if (!escapedFound)
        {
            cursor--;
            if(!hasCharacter) {
                errors.createNewError(CompilerError.ILLEGAL_CHAR_LITERAL_FORMAT, line, col, ", character literals cannot be empty");
            } else
                addToken(TokenId.CHAR_LITERAL);
            cursor++;
        }
        else
        {
            entities.add(new TokenEntity(getEscapedString(tokens.substring(start, cursor-1)), TokenId.CHAR_LITERAL, col, line));
        }

        advance();
    }

    private def parseIdentifier() {
        var hasletter = false;

        while(!isEnd() && (char.isAlphaNum(current) || current == '_'))
        {
            if(char.isAlpha(current))
                hasletter = true;

            advance();
        }

        cursor--;
        if(!hasletter)
            errors.createNewError(CompilerError.GENERIC, line, col, " expected at least 1 letter in identifier");
        else
            addToken(TokenId.IDENTIFIER);
    }

    private def parseNumber() {
        if(current == '0' && peek(1) == 'x') {
            var underscoreAllowed = false;

            col += 2; cursor += 2;
            while(!isEnd() && (isHexNum(current) || current == '_')) {
                if(isHexNum(current))
                    underscoreAllowed = true;
                else {
                    if(!underscoreAllowed) {
                        errors.createNewError(CompilerError.ILLEGAL_NUMBER_FORMAT, line, col, ", unexpected or illegally placed underscore");
                        break;
                    }

                    advance();
                    continue;
                }

                advance();
            }

            cursor--;
            addToken(TokenId.HEX_LITERAL);
            return;
        } else {
            /*
               Attempt to match a valid numeric value in one of the following formats:
               1. 123456
               2. 123.456
               3. 1_23.456e3
               4. 123.456E3
               5. 123.456e+3
               6. 123.456E+3
               7. 123.456e-3
               8. 123.456E-3
               9. 12345e5
            */
            var dotFound          = false;
            var eFound            = false;
            var postESignFound    = false;
            var underscoreAllowed = false;

            while(!isEnd())
            {
               if('_' == current) {
                   if(!underscoreAllowed || peek(-1) == '.')
                   {
                       errors.createNewError(CompilerError.ILLEGAL_NUMBER_FORMAT, line, col, ", unexpected or illegally placed underscore");
                       return;
                   }
                   advance();
               }
               else if ('.' == current)
               {
                   if (dotFound)
                   {
                       errors.createNewError(CompilerError.ILLEGAL_NUMBER_FORMAT, line, col, ", double decimal");
                       return;
                   }
                   dotFound = true;
                   advance();
                   continue;
               }
               else if ('e' == char.toLower(current))
               {
                   underscoreAllowed = false;
                   var c = peek(1);
                   if (peekEnd(1))
                   {
                       errors.createNewError(CompilerError.ILLEGAL_NUMBER_FORMAT, line, col, ", missing exponent prefix");
                       return;
                   }
                   else if (
                           ('+' != c) &&
                           ('-' != c) &&
                           !char.isDigit(c)
                           )
                   {
                       errors.createNewError(CompilerError.ILLEGAL_NUMBER_FORMAT, line, col, ", expected `+`, `-`, or digit");
                       return;
                   }
                   eFound = true;
                   advance();
                   continue;
               }
               else if (eFound && isSign(current))
               {
                   if (postESignFound)
                   {
                       errors.createNewError(CompilerError.ILLEGAL_NUMBER_FORMAT, line, col, ", duplicate exponent sign postfix");
                       return;
                   }
                   postESignFound = true;
                   advance();
                   continue;
               }
               else if (('.' != current) && !char.isDigit(current))
                   break;
               else
               {
                   if(char.isDigit(current) && !eFound)
                       underscoreAllowed = true;
                   advance();
               }
            }

            cursor--;
            addToken(TokenId.INTEGER_LITERAL);
        }
    }

    private def parseString() {
        string message = "";
        if (tokensLeft() < 2)
        {
            errors.createNewError(CompilerError.EXPECTED_STRING_LITERAL_EOF, line, col);
            advance();
            return;
        }
        advance();

        start = cursor;
        var escapedFound = false;
        var escaped = false;

        while (!isEnd())
        {
            if(char.isNewLine(current))
            {
                errors.createNewError(CompilerError.ILLEGAL_STRING_FORMAT, line, col, ", expected `\"` before end of line");
                newline();
                return;
            }
            else if (!escaped && ('\\' == current))
            {
                escapedFound = true;
                escaped = true;
                advance();
                continue;
            }
            else if (!escaped)
            {
                if ('"' == current)
                    break;
            }
            else if (escaped)
            {
                if(!char.isAlpha(current) && ('\\' != current)  && ('\"' != current) && ('\'' != current))
                {
                    errors.createNewError(CompilerError.ILLEGAL_STRING_FORMAT, line, col, ", text preceding `\\` must be alpha, '\\', '\\'', or '\"' only");
                    return;
                }

                escaped = false;
            }

            advance();
        }

        if (isEnd())
        {
            errors.createNewError(CompilerError.UNEXPECTED_EOF, line, col);
            return;
        }

        cursor--;
        if (!escapedFound)
            addToken(TokenId.STRING_LITERAL);
        else
            entities.add(new TokenEntity(getEscapedString(tokens.substring(start, cursor)), TokenId.STRING_LITERAL, col, line));
        cursor++;
    }

    def isEnd() : var {
        return (cursor>=tokens.size());
    }

    def isWhitespace(var c) : var {
        return ((' '  == c) || ('\n' == c) ||
                ('\r' == c) || ('\t' == c) ||
                ('\b' == c) || ('\v' == c) ||
                ('\f' == c));
    }

    def getEscapedString(string msg) : string {
        string escapedMessage = "";
        char c = 0;
        for(var i = 0; i < msg.size(); i++)
        {
            if(msg.at(i) == '\\')
            {
                switch(msg.at(i+1)) {
                    case 'n':
                        escapedMessage += '\n';
                        break;
                    case 't':
                        escapedMessage += '\t';
                        break;
                    case 'b':
                        escapedMessage += '\b';
                        break;
                    case 'v':
                        escapedMessage += '\v';
                        break;
                    case 'r':
                        escapedMessage += '\r';
                        break;
                    case 'f':
                        escapedMessage += '\f';
                        break;
                    case '0':
                        escapedMessage += '\0';
                        break;
                    case 'a':
                        escapedMessage += '\a';
                        break;
                    default:
                        escapedMessage += c.char(msg.at(i+1));
                        break;
                }

                i++;
            }
            else
                escapedMessage += c.char(msg.at(i));
        }
        return escapedMessage;
    }

    def isSign(var c) : var {
        return (('+'  == c) || ('-' == c));
    }

    def newline() {
        col = 0;
        line++;
    }

    def advance() {
        col++;
        cursor++;
        try {
            current = tokens.at(cursor);
        } catch(Exception e) {}
    }

    def peekEnd(var chars) : var {
        return (cursor+chars) >= tokens.size();
    }

    def tokensLeft() : var {
        return tokens.size() - cursor;
    }

    def peek(var chars) : var {
        return ((cursor+chars) >= tokens.size() || (cursor+chars) < 0)
            ? tokens.at(tokens.size()-1) : tokens.at(cursor+chars);
    }

    def isSymbol(var c) : var {
        return (('+' == c) || ('-' == c) ||
                ('*' == c) || ('/' == c) ||
                ('^' == c) || ('<' == c) ||
                ('>' == c) || ('=' == c) ||
                (',' == c) || ('!' == c) ||
                ('(' == c) || (')' == c) ||
                ('[' == c) || (']' == c) ||
                ('{' == c) || ('}' == c) ||
                ('%' == c) || (':' == c) ||
                ('?' == c) || ('&' == c) ||
                ('|' == c) || (';' == c) ||
                ('!' == c) || ('.' == c) ||
                ('#' == c) || ('$' == c));
    }

    public def getErrors() : ErrorManager {
        return errors;
    }

    public def getData() : string {
        return tokens;
    }

    public def getLines() : ArrayList<string> {
        return lines;
    }

    public def getFile() : string {
        return file;
    }

    public def get(var pos) : TokenEntity {
        return entities.get(pos);
    }

    public def last() : TokenEntity {
        return entities.last(); // EOF returned
    }

    public def size() : var {
        return entities.size();
    }

    def addLines() {
        var[] raw = tokens.getData();

        start = 0;
        for(var i = 0; i < sizeof(raw); i++) {
            if(char.isNewLine(raw[i])) {
                lines.add(tokens.substring(start, i==0 || start==i ? i : i-1));
                start = i+1;
            }
        }

        if(start==0)
            lines.add(new string(raw));
    }

    def isHexNum(var c) : var {
        return (char.isDigit(c) || ((c >= 65) && (c <= 72)) ||
                       ((c >= 97) && (c <= 104)));
    }

    private ArrayList<TokenEntity> entities;
    private ErrorManager errors;
    private ArrayList<string> lines;
    private string tokens = "";
    private var cursor, start;
    private var current;
    private var line;
    private var col;
    private string file = "";
}