mod std.io;

import std;

/**
* IOStream represents an abstract representation of a stream
* or "buffer" of data. This class is responsible for reading and
* writing to a file. this class uses the file class to perform the IO
* operation and does not catch any exception occurred as a result of the
* reading and writing operations performed.
*
* This class uses a buffered concept to write to files in 1 payload as writing
* single bytes to a file is expensive. In order to successfully write to a file
* the IOStream class must be closed to send and flush the buffer. Once an IO buffer
* is closed it can be reused however it is not recommended as the buffer will be empty
*/
public class IOStream {
    private string data = new string();
    private File file;
    private var direction;
    private IOStream.StreamReader reader;

    public static var READ = 1;
    public static var WRITE = 0;


    class StreamReader {
        private var pos;
        private string mData;

        protected StreamReader(string contents) {
            mData = contents;
        }

        public def next() : var {
            if(pos >= mData.size())
                return -1;
            return mData.at(pos++);
        }

        public def nextLine() : string {
            var start = pos;
            var ch;
            do {
                ch = next();
                if(ch==-1) break;
                else if(char.isNewLine(ch))
                    break;
            }while(true);

            return mData.substring(start, pos-1);
        }
    }

    private IOStream() {} // to prevent user from calling this

    public IOStream(File file, var direction) {
        self->file = file;
        self->direction = direction;

        if(direction==READ) {
            File.readAllText(file, data);
            reader = new IOStream.StreamReader(data);
        }
    }

    public def close() {
        if(direction==WRITE) {
            File.write(file, data);
        }
        data = "";
    }

    /**
    * A buffer can be read 1 char at a time from a file through
    * the use of this function
    */
    public def nextChar() : var {
        if(direction == READ)
            return reader.next();
        else
            throw new IllegalStateException("illegal read on IOStream");
    }

    /**
    * A buffer can be read 1 line at a time from a file through
    * the use of this function
    */
    public def nextLine() : string {
        if(direction == READ)
            return reader.nextLine();
        else
            throw new IllegalStateException("illegal read on IOStream");
    }

    public def size() : var {
        return data.size();
    }

    public def write(string data) {
        if(direction == READ)
            throw new IllegalStateException("illegal write on IOStream");
        self->data += data;
    }

    public def write(var n) {
        if(direction == READ)
            throw new IllegalStateException("illegal write on IOStream");
        data += string.parse(n);
    }

    public def writeChar(var n) {
        if(direction == READ)
            throw new IllegalStateException("illegal write on IOStream");
        data += n;
    }

    public def clear() {
        data = "";
    }

    public def toString() : string {
        return new string(data);
    }

    public def write(var[] data) {
        if(direction == READ)
            throw new IllegalStateException("illegal write on IOStream");
        self->data += data;
    }

}