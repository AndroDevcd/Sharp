mod std;

/**                          
* Created by bknun on 6/7/2018            
* Description:                             
*/
public class ArrayList<T> {
    private T[] mData;

    private static const var DEFAULT_CAPACITY = 10;
    private static const var RESIZE_CAPACITY = 20;

    /**
    * Size and capacity to keep allocations at a low
    */
    private var capacity;
    private var size;

    /**
     * Constructs an empty list.
     * with a default capacity of 10
     */
    public ArrayList() {
        /* data will be set to default capacity */
        capacity = DEFAULT_CAPACITY;
        mData = new T[DEFAULT_CAPACITY];
    }

    /**
     * Returns the number of elements in this list.
     *
     */
    public def size() : var {
        return size;
    }

    /**
     * Returns true if this list contains no elements.
     *
     */
    public def empty() : var {
        return size == 0;
    }

    /**
     * Returns an array containing all of the elements in this list
     * in proper sequence (from first to last element).
     *
     */
    public def toArray() : T[] {
        if(size == 0) return null;
        return ((T[]) System.copyOf(mData, size));
    }

    /**
     * Returns the element at the specified position in this list.
     *
     */
    public def get(var index) : T {
        if (index >= size)
            throw new IndexOutOfBoundsException(outOfBoundsMsg(index));

        return mData[index];
    }

    /**
     * Returns the last element in this list.
     *
     */
    public def last() : T {
        return get(size()-1);
    }

    /**
     * Replaces the element at the specified position in this list with
     * the specified element.
     *
     */
    public def set(var index, T element) : T {
        if (index >= size)
            throw new IndexOutOfBoundsException(outOfBoundsMsg(index));

        T oldValue = mData[index];
        mData[index] = element;
        return oldValue;
    }

    /**
     * Expand the list to hold more data
     *
     * The list is oversized to reduce the ammount of allocations
     * required by the system
     */
    private def expand() {
        if(size >= capacity) {
            // resize
            capacity += (capacity >> 2) + RESIZE_CAPACITY;
            System.realloc(mData, capacity);
        }
    }

    /**
     * Shrink the list to hold more data
     */
    private def shrink() {
        if(size <= capacity && size > 0) {
            size--;
            capacity--;
            System.realloc(mData, capacity);
        }
    }

    /**
      * Appends the specified elements inside the provided list to the end of this list.
      *
      * @param e elements to be appended to this list
      * @return <tt>true</tt> (as specified by {@link Collection#add})
      */
     public def addAll(ArrayList<T> lst) {
        T[] arry = lst.toArray();
        foreach(T data : arry) {
            add(data);
        }
     }

    /**
      * Appends the specified element to the end of this list.
      *
      * @param e element to be appended to this list
      * @return <tt>true</tt> (as specified by {@link Collection#add})
      */
     public def add(T e) {
         expand();
         mData[size++] = e;
     }


    /**
      * Shifts the list back 1 element
      *
      * @param e element to be appended to this list
      * @return <tt>true</tt> (as specified by {@link Collection#add})
      */
     public def popBack() {
         shrink();
     }

    /**
     * Inserts the specified element at the specified position in this
     * list. Shifts the element currently at that position (if any) and
     * any subsequent elements to the right (adds one to their indices).
     *
     */
     public def insert(var pos, T value) {
         if(pos>size || pos < 0)
            throw new IndexOutOfBoundsException(outOfBoundsMsg(pos));

         if(size == 0) {
             add(value);
         } else {
            expand();
            size++;
             for(var i = size - 1; i > pos; i--)
                mData[i] = mData[i-1];
             mData[pos] = value;
         }

     }

    /**
     * Removes the element at the specified position in this list.
     * Shifts any subsequent elements to the left (subtracts one from their
     * indices).
     *
     */
     public def remove(var pos) {
         if(pos>size || pos < 0)
            throw new IndexOutOfBoundsException(outOfBoundsMsg(pos));

         if(size==1){
             size--;
         }
         else if(size==2) {
             if(pos==0) {
                 mData[0]=mData[1];
             }
             shrink();
         } else {
             var newLen=size-1;
             for(var i = pos; i < newLen; i++)
                 mData[i] = mData[i+1];

             shrink();
         }
     }

     /**
      * Removes all of the elements from this list.  The list will
      * be empty after this call returns.
      */
     public def clear() {
         // clear to let GC do its work
         size = 0;
         capacity = DEFAULT_CAPACITY;
         mData = new T[DEFAULT_CAPACITY];
     }

    /**
     * Constructs an IndexOutOfBoundsException detail message.
     * Of the many possible refactorings of the error handling code,
     * this "outlining" performs best with both server and client VMs.
     */
    private def outOfBoundsMsg(var index) : string {
        return "Index: " + index + ", size: " + size;
    }

}