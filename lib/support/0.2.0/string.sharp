mod std;

/**
 * Created by BraxtonN on 5/17/2018.
 */
public class string {
    /*this will hold our string */
        private var[] data;

        public string( var[] immstr )
        {
            if(immstr == null) {
                data = null;
            } else {
                append(immstr);
            }

        }

        public def replace( var pos, var replaceChar ) : string
        {
            if(pos >= sizeof(data)) {
                throw new RuntimeErr("Index out of bounds exception");
            }

            data[pos] = replaceChar;
            return self;
         }

        def append(var char) : string
        {
            var[] str = new var[] { char };
            return append(str);
        }

        def append(var[] immstr) : string
        {
            var[] newStr = new var[sizeof(data) + sizeof(immstr)];

            var iter=0;
            for(var i = 0; i < sizeof(data); i++) {
                newStr[iter++] = (_int16)data[i];
            }

            for(var i = 0; i < sizeof(immstr); i++)
            {
                newStr[iter++] = (_int16)immstr[i];
            }

            data = newStr;
            return self;
        }

        public def at( var pos ) : var
        {
            if(pos >= sizeof(data)) {
                throw new RuntimeErr("Index out of bounds Array: size " + sizeof(data)
                       + " at index pos " + pos);
            }

            return data[pos];
        }

        public def operator+( var[] immstr ) : string
        {
            string s = new string(data);
            return s.append(immstr);
        }

        public def operator+( string str ) : string
        {
            string s = new string(data);
            if(str != null)
                return s.append(str.toArray());
            else
                return s.append("null");
        }

        public def operator==( string str ) : var
        {
            if(sizeof(str) != sizeof(data)) {
                return false;
            }

            var iter;
            foreach(var char : data) {
                if(char != str.at(iter++)) {
                    return false;
                }
            }

            return true;
        }

        public def operator==( var[] str ) : var
        {
            if(sizeof(str) != sizeof(data)) {
                return false;
            }

            var iter;
            foreach( var char : data ) {
                if(char != str[iter++]) {
                    return false;
                }
            }

            return true;
        }

        public def operator=( var[] immstr )
        {
            data = Array.copyOf(immstr, sizeof(immstr));
        }

        public def operator+( var num ) : string
        {
            string s = new string(data);
            s.append(parse(num).toArray());

            return s;
        }

        public def operator+=( var num ) : string
        {
            return append(parse(num).toArray());
        }

        public def operator+=( var[] immstr ) : string
        {
            return append(immstr);
        }

        public def operator+=( string str ) : string
        {
            return append(str.toArray());
        }

        public def toArray() : var[]
        {
            return Array.copyOf(data, sizeof(data));
        }

        public static def parse( var number ) : string
        {
            number = (_int64)number;
            if (number == 0) {
                return new string("0");
            }

            string temp = new string("");
            string returnvalue= new string("");
            var negative = false;
            if(((_int64)number)<0) {
                number *= -1;
                negative = true;
            }

            while (((_int64)number)>0) {
                temp.append((number%10)+48);
                number/=10;
            }

            if(negative) returnvalue.append("-");
            for (var i=sizeof(temp.toArray())-1; i >= 0; i--) {
                returnvalue.append(temp.at(i));
            }
            return returnvalue;
        }

        public def substring(var startIndex) : string {
            if((startIndex >= size()) || (startIndex < 0))
                return new string("");


            string s = "";
            for(var i = startIndex; i < size(); i++) {
                s.append(at(i));
            }

            return s;
        }

        public static def parseInt(string s) : var {
            var i = 0;
            var number = 0;
            var isNegative = false;
            var len = s.size();
            if( s.at(0) == '-' ){
                isNegative = true;
                i = 1;
            }
            while( i < len ){
                if(!isDigit(s.at(i))) {
                    throw new IllegalArgumentException(
                        "invalid char in string: " + (new var[] { s.at(i) }));
                }

                number *= 10;
                number += ( s.at(i++) - '0' );
            }
            if( isNegative )
                number = -number;
            return number;
        }

        public static def toString(var n) : string {
            if (n == 0) return new string("0");
            string sb = new string();

            if(((_int8)n) < 0) {
                sb += "-";
                n *= -1;
            }
            while (((_int8)n) > 0) {
                var curr = n % 10;
                n /= 10;
                sb += curr;
            }

            return sb.substring(0);
        }

        private static def isDigit(var c) : var {
            return (c>='0') && (c<='9');
        }
        
        public def size() : var
        {
            return sizeof(data);
        }
}
